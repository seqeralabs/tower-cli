plugins {
    id 'java'
    id 'application'
    id 'com.palantir.graal' version "0.9.0"
}

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenLocal()
    jcenter()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'ch.qos.logback:logback-core:1.2.3'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'info.picocli:picocli:4.5.2'
    annotationProcessor 'info.picocli:picocli-codegen:4.5.2'

    implementation 'io.seqera:tower-java-sdk:1.1.0'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.27'
    implementation 'org.codehaus.groovy:groovy:3.0.5'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.compilerArgs += ["-Aproject=${project.name}"]
}

application {
    // Define the main class for the application.
    mainClassName = 'io.seqera.tower.cli.App'
    applicationDefaultJvmArgs = ["-agentlib:native-image-agent=config-output-dir=conf/"]
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}

graal {
    graalVersion '21.1.0'
    mainClass 'io.seqera.tower.cli.App'
    outputName 'towr'
    option '--no-fallback'
    option '--no-server'
    option '--allow-incomplete-classpath'
    option '--report-unsupported-elements-at-runtime'
    option '-H:+AddAllCharsets'
    option '-H:EnableURLProtocols=https'
    option '-H:ConfigurationFileDirectories=conf/'
    option '-H:+ReportExceptionStackTraces'
}
