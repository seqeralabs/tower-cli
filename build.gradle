plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    alias(libs.plugins.cadixdevLicenser)
    alias(libs.plugins.graalvmNative)
    alias(libs.plugins.shadow)
}

// define these in the `gradle.properties` file
ext.githubUser = project.findProperty('github.packages.user')
ext.githubToken = project.findProperty('github.packages.token')

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/seqeralabs/tower-java-sdk")
        credentials {
            username = githubUser ?: System.getenv("GITHUB_USERNAME")
            password = githubToken ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    implementation(libs.javaxAnnotation)
    implementation(libs.jakartaAnnotation)
    implementation(libs.slf4jApi)
    implementation(libs.logbackCore)
    implementation(libs.logbackClassic)

    implementation(libs.towerJavaSdk)
    // Upgrade transitive Jersey client dependencies to non-vulnerable 2.x version
    implementation(libs.jerseyClient)
    implementation(libs.jerseyMediaMultipart)
    implementation(libs.jerseyMediaJsonJackson)
    implementation(libs.jerseyHk2)

    implementation(libs.picocli)
    implementation(libs.commonsCompress)
    implementation(libs.xz)
    implementation(libs.classgraph)
    annotationProcessor(libs.picocliCodegen)

    testImplementation(libs.mockserverClientJava)
    testImplementation(libs.mockserverNetty)
    testImplementation(libs.mockserverJunitJupiter)
    // Upgrade transitive mock-server dependencies to non-vulnerable 2.x version
    testImplementation(libs.commonsIo)

    testImplementation(libs.junitJupiterApi)
    testImplementation(libs.junitJupiterParams)
    testRuntimeOnly(libs.junitJupiterEngine)

}

license {
    header = project.file('HEADER.txt')
    properties {
        name = 'Seqera'
        year = '2021-2023'
    }
    exclude '**/*.properties'
    exclude 'gradlew'
    exclude 'conf/**'
}

application {
    mainClass.set('io.seqera.tower.cli.Tower')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

graalvmNative {
    toolchainDetection = true
    binaries {
        main {
            imageName = 'tw'
            mainClass = 'io.seqera.tower.cli.Tower'
            configurationFileDirectories.from(file('conf'))

            if (System.env.getOrDefault("PLATFORM", "") == "linux-x86_64") {
                buildArgs(['--static', '--libc=musl', '--gc=G1', '-march=compatibility'])
            }
            buildArgs.add('--allow-incomplete-classpath')
            buildArgs.add('--report-unsupported-elements-at-runtime')
            buildArgs.add('-H:+AddAllCharsets')
            buildArgs.add('-H:EnableURLProtocols=https,http')
            buildArgs.add('-H:+ReportExceptionStackTraces')

            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(21)
                vendor = JvmVendorSpec.matching("Oracle Corporation")
            }

        }

        test {
            verbose = true
        }
    }
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

tasks.register('buildInfo') {
    doLast {
        def version = rootProject.file('VERSION').text.trim()
        def versionApi = rootProject.file('VERSION-API').readLines().get(0).trim()
        def commitId = System.env.getOrDefault("GITHUB_SHA", "unknown").substring(0, 7)
        def platform = System.env.getOrDefault("PLATFORM", "java")
        def info = """\
                    version=${version}
                    versionApi=${versionApi}
                    commitId=${commitId}
                    platform=${platform}
                """.stripIndent().toString()
        def f = file("src/main/resources/META-INF/build-info.properties")
        f.parentFile.mkdirs()
        f.text = info
    }
}

tasks.named('compileJava') {
    options.release = 11
    options.compilerArgs += ["-Aproject=${project.name}"]
    dependsOn tasks.named('buildInfo')
}

tasks.named('run') {
    // Pass CLI arguments via: `./gradlew run --args='arg1 arg2'`, or via: `./gradlew run -Pargs='arg1 arg2'`
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }

    // Run with GraalVM Tracing Agent enabled
    if (System.getProperty('tracing-agent')?.toBoolean()) {
        jvmArgs = ["-agentlib:native-image-agent=access-filter-file=conf/access-filter-file.json,config-merge-dir=conf/"]
    }
}

tasks.register('runReflectionConfigGenerator', JavaExec) {
    group = 'build'
    description = 'Run ReflectionConfigGenerator for Tower SDK with native-image-agent'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.seqera.tower.cli.utils.config.ReflectionConfigGenerator'
    jvmArgs = ["-agentlib:native-image-agent=access-filter-file=conf/access-filter-file.json,config-merge-dir=conf/"]
}

tasks.named('shadowJar') {
    archiveBaseName.set('tw')
    archiveClassifier.set('')
    archiveVersion.set('')
}

tasks.named('test') {
    // Use junit platform for unit tests
    useJUnitPlatform()
    dependsOn tasks.named('checkLicenses')
    // Run tests with GraalVM Tracing Agent enabled
    if (System.getProperty('tracing-agent')?.toBoolean()) {
        jvmArgs = ["-agentlib:native-image-agent=access-filter-file=conf/access-filter-file.json,config-merge-dir=conf/"]
    }
}

tasks.named('jacocoTestReport') {
    dependsOn tasks.named('test') // tests are required to run before generating the report
}
